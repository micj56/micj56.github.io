//
// input.spl
//


type Line = array [80] of int;


proc readLine(ref line: Line) {
  var i: int;
  var c: int;

  i := 0;
  while (i < 79) {
    readc(c);
    //printi(c);
    //printc('\n');
    if (c >= ' ') {
      printc(c);
      line[i] := c;
      i := i + 1;
    } else
    if (c = 0x0D) {
      printc(c);
      printc('\n');
      line[i] := 0;
      i := 79;
    } else
    if (c = 0x08) {
      if (i > 0) {
        printc(0x08);
        printc(' ');
        printc(0x08);
        i := i - 1;
      }
    }
  }
  line[79] := 0;
}


proc readInt(ref line: Line, ref ptr: int, ref i: int) {
  var needMore: int;
  var sign: int;

  needMore := 1;
  while (needMore = 1) {
    while (line[ptr] = ' ') {
      ptr := ptr + 1;
    }
    if (line[ptr] = 0) {
      readLine(line);
      ptr := 0;
    } else {
      needMore := 0;
    }
  }
  if (line[ptr] = '-') {
    sign := -1;
    ptr := ptr + 1;
  } else {
    sign := 1;
  }
  i := 0;
  needMore := 1;
  while (needMore = 1) {
    if (line[ptr] >= '0') {
      if (line[ptr] <= '9') {
        i := i * 10 + line[ptr] - '0';
        ptr := ptr + 1;
      } else {
        needMore := 0;
      }
    } else {
      needMore := 0;
    }
  }
  i := i * sign;
}


proc main() {
  var line: Line;
  var ptr: int;
  var val: int;

  ptr := 0;
  line[0] := 0;
  while (1 = 1) {
    readInt(line, ptr, val);
    printi(val);
    printc('\n');
  }
}
